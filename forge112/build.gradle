def forgeVersion = '1.12.2-14.23.5.2860'
def forgeMappings = '20171003-1.12'
buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:3.+'
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

minecraft {
    mappings channel: 'snapshot', version: forgeMappings
}

repositories {
    maven { url = 'https://maven.minecraftforge.net/' }
    maven { url = "https://repo.spongepowered.org/maven" }
}

dependencies {
    minecraft "net.minecraftforge:forge:$forgeVersion"
    compile project(':core')
    compile 'org.spongepowered:spongeapi:6.0.0-SNAPSHOT'
    compile 'com.sk89q.worldedit:worldedit-forge-mc1.8.9:6.1.1'
}

shadowJar {
    relocate 'org.yaml.snakeyaml', 'com.boydti.fawe.yaml'
    dependencies {
        include(dependency('com.github.luben:zstd-jni:1.1.1'))
        include(dependency('co.aikar:fastutil-lite:1.0'))
        include(dependency(':core'))
        include(dependency('org.yaml:snakeyaml:1.16'))
    }
    archiveName = "${parent.name}-${project.name}-${parent.version}.jar"
    destinationDir = file '../target'
    manifest {
        attributes("Main-Class": "com.boydti.fawe.installer.InstallerFrame")
    }
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'version': rootProject.version,
                'mcVersion': forgeVersion
        exclude 'mcmod.info'
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

sourceSets {
    main {
        output.resourcesDir = output.classesDir
    }
}
